import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

plugins {
    id 'com.intershop.gradle.javacc' version '4.1.3'
}

configurations {
    frontend
    frontend_libs
    hub_libs
}

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    implementation 'org.slf4j:slf4j-simple:1.7.25'
    implementation 'io.github.microutils:kotlin-logging:1.7.9'

    frontend project(path: ':entity-browser-frontend', configuration: 'frontend_build')

    implementation "org.jetbrains.xodus:xodus-environment:$xodusVersion"
    implementation "org.jetbrains.xodus:xodus-entity-store:$xodusVersion"
    implementation "org.jetbrains.xodus:xodus-openAPI:$xodusVersion"
    implementation "org.jetbrains.xodus:xodus-crypto:$xodusVersion"

    implementation "io.ktor:ktor-server-core:$ktorVersion"
    implementation "io.ktor:ktor-server-jetty:$ktorVersion"
    implementation "io.ktor:ktor-server-default-headers:$ktorVersion"
    implementation "io.ktor:ktor-server-status-pages:$ktorVersion"
    implementation "io.ktor:ktor-server-content-negotiation:$ktorVersion"
    implementation "io.ktor:ktor-server-compression:$ktorVersion"
    implementation "io.ktor:ktor-serialization-jackson:$ktorVersion"

    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-inline:5.2.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.squareup.retrofit2:retrofit:2.11.0'
    testImplementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    testImplementation 'com.squareup.okhttp3:okhttp:4.12.0'
    testImplementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

}

tasks.register('buildFrontend'){
    dependsOn(project.configurations.frontend)
}

tasks.register('runShadow', JavaExec) {
    classpath(shadowJar.archiveFile)
}

shadowJar {
    dependsOn = ['buildFrontend']
    archiveBaseName.set("xodus-entity-browser")
    archiveVersion.set(null)
    getDestinationDirectory().set(globalArtifactsBuildDir)
    manifest {
        attributes 'Main-Class': 'jetbrains.xodus.browser.web.MainKt'
    }
    transform(ServiceFileTransformer) {
        path = 'META-INF/services'
        include 'org.eclipse.jetty.http.HttpFieldPreEncoder'
    }
    from { project.configurations.frontend.collect { zipTree(it) } }
}

artifacts {
    archives shadowJar
}

javacc {
    configs {
        template {
            inputFile = file('src/main/javacc/smart-search.jj')
            packageName = 'jetbrains.xodus.browser.web.search'
            lookahead = '2'
        }
    }
}