buildscript {
    ext.xodusVersion = '3.0.131'
    ext.kotlinVersion = '1.8.10'
    ext.ktorVersion = '1.6.7'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'idea'                                         // ide sync fine tuning
    id 'com.github.johnrengelman.shadow'
}

apply plugin: 'kotlin'

ext.globalArtifactsBuildDir = buildDir



allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    sourceCompatibility = 11
    group = 'org.jetbrains.xodus'
    version = '3.0.0' + ((project.hasProperty("teamcity")) ? project.teamcity["build.number"] : '')

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'https://packages.jetbrains.team/maven/p/xodus/xodus-daily' }
        // necessary for dokka 1.4.30
        maven { url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
    }

    idea {
        module {
            downloadSources = true
        }
    }

    test {
        minHeapSize = '512m'
        maxHeapSize = '512m'
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        outputs.upToDateWhen { false }
    }

    tasks.register('sourceJar', Jar) {
        archiveClassifier = 'sources'
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        includeEmptyDirs false
        from project.sourceSets.main.java
        from project.sourceSets.main.kotlin
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = '11'
            apiVersion = '1.8'
            languageVersion = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = '11'
            apiVersion = '1.8'
            languageVersion = '1.8'
        }
    }

}

defaultTasks 'clean build'

repositories {
    mavenCentral()
}

apply from: 'publishing.gradle'