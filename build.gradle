buildscript {
    ext.xodusVersion = '9.9.180'
    ext.kotlinVersion = '2.1.0'
    ext.ktorVersion = '2.3.11'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'idea'                                         // ide sync fine tuning
    id 'com.github.johnrengelman.shadow'
}

apply plugin: 'kotlin'

ext.globalArtifactsBuildDir = layout.buildDirectory.get().asFile

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    group = 'org.jetbrains.xodus'
    version = '3.0.' + ((project.hasProperty("teamcity")) ? project.teamcity["build.number"] : '999')

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://packages.jetbrains.team/maven/p/xodus/xodus-daily' }
        // necessary for dokka 1.4.30
        maven { url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
        maven { url 'https://packages.jetbrains.team/maven/p/xodus/youtrackdb-daily' }
    }

    idea {
        module {
            downloadSources = true
        }
    }

    test {
        minHeapSize = '512m'
        maxHeapSize = '512m'
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        outputs.upToDateWhen { false }
    }

    tasks.register('sourceJar', Jar) {
        archiveClassifier = 'sources'
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
        includeEmptyDirs false
        from project.sourceSets.main.java
        from project.sourceSets.main.kotlin
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.slf4j') {
                details.useVersion "2.0.7"
            }
        }
    }

}

defaultTasks 'clean build'

repositories {
    mavenCentral()
}

apply from: 'publishing.gradle'
