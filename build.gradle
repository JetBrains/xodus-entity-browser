buildscript {
    ext.xodusVersion = '1.3.135'
    ext.kotlinVersion = '1.3.21'
    ext.ktorVersion = '1.2.5'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'idea'                                         // ide sync fine tuning
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

apply plugin: 'kotlin'

ext.globalArtifactsBuildDir = buildDir

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    sourceCompatibility = 1.8
    group = 'org.jetbrains.xodus'
    version = '1.3.0' + ((project.hasProperty("teamcity")) ? project.teamcity["build.number"] : '')

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'https://jetbrains.bintray.com/xodus' }
        maven { url 'https://kotlin.bintray.com/kotlinx' }
    }

    idea {
        module {
            downloadSources = true
        }
    }

    test {
        minHeapSize = '512m'
        maxHeapSize = '512m'
        testLogging {
            showStandardStreams = true
            exceptionFormat = 'full'
        }
        outputs.upToDateWhen { false }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        duplicatesStrategy 'exclude'
        includeEmptyDirs false
        from project.sourceSets.main.java
        from project.sourceSets.main.kotlin
    }

}

defaultTasks 'clean build'

repositories {
    mavenCentral()
}

apply from: 'publishing.gradle'