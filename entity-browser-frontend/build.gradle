plugins {
    id "com.github.node-gradle.node" version "2.2.4"
}

configurations {
    frontend_build
}

node {
    download = true
    version = new File("$projectDir/.nvmrc").text.trim()
}

tasks.register('cleanFrontend', Delete) {
    delete 'dist'
}

tasks.register('runNpm', NpmTask) {
    dependsOn cleanFrontend
    args = ["run", "build"]
}


tasks.register('webContent', Copy) {
    dependsOn 'runNpm'
    from 'dist'
    into 'build/content/entity/browser/static'
}

tasks.register('jarFrontend', Jar) {
    dependsOn 'webContent'
    from 'build/content'
}

artifacts {
    frontend_build file: jarFrontend.archiveFile.get().asFile, builtBy: jarFrontend
}

clean {
    delete 'dist'
    delete 'build'
}

idea {
    module {
        // make Idea mark some dirs as excluded on module import
        excludeDirs += [file('node_modules'), file('dist'), file('.tmp')]
    }
}
