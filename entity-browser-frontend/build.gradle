plugins {
    id "com.github.node-gradle.node" version "7.1.0"
}

configurations {
    frontend_build
}

node {
    download = true
    version = new File("$projectDir/.nvmrc").text.trim()
}

tasks.withType(NpmTask).configureEach {
    environment = [
            'PUPPETEER_SKIP_DOWNLOAD': 'true'
    ]
}

tasks.register('cleanFrontend', Delete) {
    delete 'dist'
}

jar {
    dependsOn('webContent')
    from 'build/content'
}

tasks.register('runNpm', NpmTask) {
    dependsOn cleanFrontend
    args = ["run", "build"]
}


tasks.register('webContent', Copy) {
    dependsOn 'runNpm'
    from 'dist'
    into 'build/content/entity/browser/static'
}

artifacts {
    frontend_build file: jar.archiveFile.get().asFile, builtBy: jar
}

clean {
    delete 'dist'
    delete 'build'
}

idea {
    module {
        // make Idea mark some dirs as excluded on module import
        excludeDirs += [file('node_modules'), file('dist'), file('.tmp')]
    }
}
